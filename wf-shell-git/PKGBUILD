# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wf-shell-git
pkgver=0.8.0r198.deffdba
pkgrel=1
pkgdesc='GTK3-based panel for the Wayfire compositor'
url=https://wayfire.org
arch=(x86_64)
license=(custom:MIT)
provides=("${pkgname%-git}")
conflicts=("${provides[@]}")
depends=(wf-config gtkmm3 gtk-layer-shell pulseaudio)
makedepends=(wayland wayland-protocols meson ninja git gobject-introspection)
source=("${pkgname}::git+https://github.com/WayfireWM/wf-shell")
sha512sums=('SKIP')

pkgver () {
    cd "${srcdir}/${pkgname}"
    (
        set -o pipefail
        version=$(grep -oP "^\sversion:\s'\K[^']*" meson.build)
        printf "${version}r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build () {
	arch-meson "${pkgname}" build -Dpulse=enabled
	ninja -C build
}

package () {
	DESTDIR="${pkgdir}" ninja -C build install
	
	install -Dm644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 "${pkgname}/wf-shell.ini.example" "${pkgdir}/usr/share/doc/${pkgname}/wf-shell.ini.example"
}
