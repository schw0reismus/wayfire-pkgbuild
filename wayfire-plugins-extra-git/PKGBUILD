# Maintainer: Librewish <librewish@gmail.com>
pkgname=wayfire-plugins-extra-git
pkgver=0.8.0.r147.a3373ec
pkgrel=1
pkgdesc="3D wayland compositor extra plugins"
arch=('any')
url="https://github.com/WayfireWM/wayfire-plugins-extra"
license=('MIT')
depends=('wayfire-git' 'glibmm' 'iio-sensor-proxy' 'pango')
makedepends=('git' 'meson' 'ninja')
optdepends=('wcm: GTK3-based configuration tool for the Wayfire compositor')
provides=("${pkgname}" "wayfire-plugins-extra")
conflicts=("wayfire-plugins-extra")
replaces=()
options=()
source=("${pkgname}::git+https://github.com/ssfdust/wayfire-plugins-extra#branch=arch" "mag.patch" "windeco.patch")
sha256sums=('SKIP' 'e15e92e570b89551f7691498d4b0a99a56e778d3bb5aad3ff32f3b4e37b26c19' '13ac9a2d51d77232596829374cfe1a4f0ee539761922bfe18987d95e00459015')
install=wayfire-plugins-extra.install

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        -Denable_dbus=false \
        -Denable_windecor=true \
        -Denable_wayfire_shadows=true \
        -Denable_wf_wallpaper=true \
        "${srcdir}/${pkgname}/" "$1"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    git submodule update --init
    patch -p1 -i "${srcdir}/mag.patch"
    patch -p1 -i "${srcdir}/windeco.patch"
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}

package() {
    DESTDIR="${pkgdir}/" meson install -C "${srcdir}/${_builddir}"
}
