# Maintainer: Librewish <librewish@gmail.com>

pkgname=wayfire-plugins-extra-git
pkgver=0.8.0r134.6258516
pkgrel=1
pkgdesc="3D wayland compositor extra plugins"
arch=('any')
url="https://github.com/WayfireWM/wayfire-plugins-extra"
license=('MIT')
depends=('wayfire-git' 'glibmm' 'iio-sensor-proxy')
makedepends=('git' 'meson' 'ninja')
optdepends=('wcm: GTK3-based configuration tool for the Wayfire compositor')
provides=("${pkgname}" "wayfire-plugins-extra")
conflicts=("wayfire-plugins-extra")
replaces=()
options=()
source=("${pkgname}::git+https://github.com/WayfireWM/wayfire-plugins-extra" "wayfire-plugins-extra-git.patch" "./wayfire-plugins-extra-dbus-git.patch")
sha256sums=('SKIP' '7fd858f2dd3adfb25d1b3cad120953e8ab022084b46b30554f7d89f2df6bd700' '369f98079cd751e17a557f9b6434a898f00c6429cd55e72f94f74b3f253a84fe')
install=wayfire-plugins-extra.install

prepare() {
    cd "${srcdir}/${pkgname}"
    git submodule update --init --recursive
    git submodule add https://github.com/ssfdust/wf-wallpaper.git subprojects/wf-wallpaper
    patch -s -p1 < "${srcdir}/wayfire-plugins-extra-git.patch"
    patch -s -p1 < "${srcdir}/wayfire-plugins-extra-dbus-git.patch"
}

pkgver () {
    cd "${srcdir}/${pkgname%}"
    (
        set -o pipefail
        version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^']*")
        printf "${version}r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    arch-meson -Denable_dbus=true -Denable_windecor=true -Denable_wayfire_shadows=true -Denable_wf_wallpaper=true "${srcdir}/${pkgname}/" build 
    ninja -C build
}


package() {
    DESTDIR="${pkgdir}/" ninja -C build install
}
