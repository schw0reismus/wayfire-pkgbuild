# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
# Maintainer: Antonin DÃ©cimo <antonin dot decimo at gmail dot com>
pkgname=wlroots-noassert-git
pkgver=0.15.0r5182.0855cdac
pkgrel=1
license=(custom:MIT)
pkgdesc='Modular Wayland compositor library (git version)'
url=https://github.com/swaywm/wlroots
arch=(x86_64)
provides=("libwlroots.so" "wlroots=${pkgver%%.r*}" "wlroots-git")
conflicts=(wlroots wlroots-git)
depends=(
	libinput
	libxcb
	libxkbcommon
	opengl-driver
	pixman
	wayland
	xcb-util-errors
	xcb-util-renderutil
	xcb-util-wm
    seatd
	systemd
    vulkan-icd-loader
	xorg-xwayland)
makedepends=(
	git
	meson
	wayland-protocols
    vulkan-headers
	xorgproto)
source=("${pkgname}::git+${url}" "noassert.patch")
sha256sums=('SKIP' '6910365f5c01d9fd7b6fe817dd174b83d9a0bf16a67e7bffd7feb1acdbd700b9')

pkgver () {
    cd "${pkgname}"
    (
        set -o pipefail
        version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^']*")
        printf "${version}r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 < "${srcdir}/noassert.patch"
}

build () {
	arch-meson \
		--buildtype=release \
		-Dwerror=false \
		-Dexamples=false \
		"${pkgname}" build
	meson compile -C build
}

package () {
	DESTDIR="${pkgdir}" meson install -C build
	install -Dm644 "${pkgname}/"LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

post_upgrade() {
  echo "Make sure to upgrade wlroots-git and sway-git together."
  echo "Upgrading one but not the other is unsupported."
}
